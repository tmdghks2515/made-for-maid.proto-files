plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.madeformaid'
version = '0.0.1-SNAPSHOT'

task sourceJar(type: Jar) {
    from("src/main/proto")
    archiveClassifier.set("sources")
}

java {
    withSourcesJar() // src/main/proto 포함
}

sourceSets {
    main {
        resources {
            // .proto 파일을 resources로 간주하여 포함
            srcDir 'src/main/proto'
            include '**/*.proto'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = 'proto-files'

            artifact(sourceJar) {
                classifier = 'proto'
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/tmdghks2515/made-for-maid.proto-files")
            credentials {
                username = project.findProperty("gpr.user") ?: ""
                password = project.findProperty("gpr.key") ?: ""
            }
        }
    }
}

tasks.named('generateMetadataFileForMavenJavaPublication') {
    dependsOn tasks.named('sourceJar')
}
